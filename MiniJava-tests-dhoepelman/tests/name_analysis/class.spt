module test-na-Class

language MiniJava
start symbol Start

setup MiniJavaStart [[
	class Main {
    public static void main(String[] args) {
      System.out.println(1);
    }
  }
]]

test forward class name resolution field [[
  class Foobar {
    [[Foo]] x;
  }

  class [[Foo]] {}
]] resolve #1 to #2

test backward class name resolution field [[
  class [[Foo]] {}

  class Foobar {
    [[Foo]] x;
  }
]] resolve #2 to #1

test class name resolution variable [[
  class [[Foo]] {}

  class Foobar {
  	public int bar() {
    	[[Foo]] x;
    	return 0;
    }
  }
]] resolve #2 to #1

test class name resolution parameter [[
  class [[Foo]] {}

  class Foobar {
  	public int bar([[Foo]] x) {
    	return 0;
    }
  }
]] resolve #2 to #1

test class name resolution return [[
  class [[Foo]] {}

  class Foobar {
  	public [[Foo]] bar() {
    	return 0;
    }
  }
]] resolve #2 to #1

test class name resolution instantiation [[
  class [[Foo]] {}

  class Foobar {
  	public int bar() {
  		Foo x;
  		x = new [[Foo]]();
    	return 0;
    }
  }
]] resolve #2 to #1


test correct program [[
  class Foo {}

  class Foobar {
    Foo x;
  }
]] 0 errors

test error on duplicate class [[
  class [[Foo]] {}

  class [[Foo]] {}
]] 2 errors

test error on missing class defintion [[
	class Foo {
		[[Bar]] x;
	}
]] 1 error

test warning on unused class [[
  class Foo {}

  class [[Foobar]] {
    Foo x;
  }
]] 1 warning