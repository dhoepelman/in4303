module analysis/namebinding
 
imports
	assignment1/MiniJava
	analysis/analysis_constructors
	
namespaces
	Class
	Method
	Field
	Variable		// Also Parameter
	
binding rules

// Program definition, scoping
Program(_,_):
	scopes Class

// Class definition, referring
MainClass(main, mainargs, _):
	defines Class main of type MainClassType()
	defines Variable mainargs
	scopes Variable
Class(c, parent, _, _):
	defines Class c of type ClassType(c)
	implicitly defines Field This() of type ClassType(c)
	imports Method, imported Method, Field, imported Field from Class parent
	scopes Method, Field
Parent(p):
	refers to Class p
ClassType(c):
	refers to Class c
NewObject(c):
	refers to Class c
This():
	refers to Field This()

// Method definiton, referring
Method(t, m, params, _, _, _):
	// TODO: Make MethodType only contain the parameter types
	defines Method m of type (t, paramtypes)
		where params has type paramtypes
	scopes Variable
// Type system required
Call(exp, callee, _):
	refers to Method callee in Class c
		where exp has type ClassType(c)

// Field and variable definiton
Field(t, f):
	defines Field f of type t
// Variable and parameter declaration
Param(t, p):
	defines Variable p of type t
Var(t, v):
	defines Variable v of type t

// Variable and Field references
VarRef(ref):
	refers to Variable ref
	otherwise refers to Field ref