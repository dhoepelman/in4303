module codegen/builders

imports
  libstratego-lib
  libstratego-aterm
  libstratego-gpp
  editor-common.generated

  lib/runtime/analysis/-
  lib/runtime/nabl/-
  lib/runtime/types/-
  lib/runtime/task/-
  lib/runtime/properties/-
  
  trans/codegen/-
  
  milestone1/-
  milestone2/-
  
  lib/jasmin/JasminXT-pp

rules
	generate-jbc:
		(selected, position, ast, path, project-path) -> (filename, result)
		where
			filename := <guarantee-extension(|"generated.j")> path
		;	jasminast := <program-to-jbc> ast
		;	result := <pp-jasminxt-string> jasminast
		 	
	generate-jbc-selection:
		(selected, position, ast, path, project-path) -> (filename, result)
		where
			filename := <guarantee-extension(|"generated.aterm")> path
		;	result := <to-jbc> selected
	
	// Continue here with "Generate Java Class Files" and the challenges seem fun too 	
	generate-class-files:
		(selected, position, ast, path, project-path) -> None()
		where
			// Determines filenames and paths
			classname	:= <mainclass-name> ast
		;	parentdir	:= <dirname> path
		;	classfname	:= <make-filename> (parentdir, classname, "class")
		;	jasminfname	:= <make-filename> (parentdir, classname, "generated.j")
		 	// Write the Jasmin assembler file
		;	<write-file> (jasminfname, <program-to-jbc;pp-jasminxt-string> ast)
		 	// Call Jasmin on the generated file
		;	<try(call)> ("java", ["-jar", $[[<project-path>]/lib/jasmin.jar], "-d", parentdir, jasminfname])
		;	<refresh-workspace-file> classfname 
		 	// Create output directory for binary files
		;	outputdir := <output-directory> ()
		;	<debug(!"Outputdir: ")> outputdir
		;	<mkdir-p> outputdir
	
	run-file-after-generation:
		(selected, position, ast, path, project-path) -> (filename, result)
		where
			filename	:= <guarantee-extension(|"result.txt")> path	 	
		;	classname	:= <mainclass-name> ast
		;	fullpath	:= <concat-strings> [project-path, "/", <dirname> path]
		;	<execute-java(|fullpath) ; process-output> classname => result
		
rules // helper rules
	write-file: (filename, content) -> <id>
	where
		file := <fopen> (filename, "w")
	;	<fputs> (content, file)
	;	<fclose> file
	;	<refresh-workspace-file> filename
	 	
	mainclass-name: Program(MainClass(cname, _, _), _) -> <strip-annos> cname
	
	make-filename: (parent, filename, ext) -> <concat-strings ; guarantee-extension(|ext)> [parent, "/", filename]
	
	// This should probably be a constant or something, but I'm a noob stratego programmer and only know rules
	output-directory: () -> $[[<project-path>]/bin_mjv]
	
	// Create directory if not exists, like mkdir -p (but doesn't create parents)
	mkdir-p-e:  path -> <id>
	where
		// First check if path exists, otherwise create
		exists	:= <file-exists ; filemode ; isdir> path
	;	<debug(!"Exists: ")> exists

strategies
	// Create directory if not exists
	mkdir-p =  (file-exists ; filemode ; isdir) <+ mkdir

rules // Below portion all shamelessly stolen from JasminXT Project	
	process-output: Output(result, "") -> result

	process-output: Output(result, error) -> $[[result] Runtime error: [error]]
		where not (<""> error)

signature constructors
	
	Output: String * String -> Term
		
strategies
	
	external execute-java(|path)