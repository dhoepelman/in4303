module codegen/codegen

imports
  
  milestone1/MiniJava
  milestone2/MiniJava.core
  lib/jasmin/JasminXT
  trans/codegen/-
  
rules	// Transform MiniJava AST into JasminXT
  
  // For debugging
  to-jbc = program-to-jbc + class-to-jbc + stmt-to-jbc + exp-to-jbc
  
  /** Program     : MainClass * List(ClassDecl) **/
  program-to-jbc: Program(mainc, classes*) -> <conc> ([<class-to-jbc> mainc],<map(class-to-jbc)>classes*)
  
  /** Class **/
  
  // MainClass   : ID * ID * Statement -> MainClass
  class-to-jbc: MainClass(cname, pname, stmt) -> JBCFile(
  	JBCHeader(
		    None()
		  , None()
		  , JBCClass([PUBLIC()], <strip-annos> cname)
		  , JBCSuper(superc)
		  , []
		  , None()
		  , None()
		  , None()
		  )
	// Fields
	, []
  	// Methods
  	,[
  		<classconstructor> superc,
  		JBCMethod(
	      [PUBLIC(), STATIC()]
	    , "main"
	    , JBCMethodDesc([Array(Reference(CRef("java/lang/String")))], Void())
	    , outputstmtjbc*
	    )
  	])
  	where
  		stmtjbc* :=  <stmt-to-jbc> stmt
  	;	superc	 := "java/lang/Object"
  	 	// TODO: Better stack limit
  	;	outputstmtjbc* := [JBCLimitStack("100"),stmtjbc*,RETURN()]
  	
  
  	// Class       : ID * ParentDecl * List(FieldDecl) * List(MethodDecl) -> ClassDecl
  	class-to-jbc:	Class(cname, parent, field*, method*) -> JBCFile(
  	JBCHeader(
		    None()
		  , None()
		  , JBCClass([PUBLIC()], <strip-annos> cname)
		  , JBCSuper(<parentname>parent)
		  , []
		  , None()
		  , None()
		  , None()
		  )
	// Fields
	, []
  	// Methods
  	,[
  		<parentname;classconstructor> parent
  	]) 
  
rules	// Helper rules
	
  parentname: None() -> "java/lang/Object"
  parentname: Parent(p)	-> <strip-annos>p
  
  mjvtype-to-jtype: IntArray() -> Array(Int())
  mjvtype-to-jtype: Bool() -> Boolean()
  mjvtype-to-jtype: Int() -> Int()
  mjvtype-to-jtype: ClassType(cname) -> Reference(CRef(<strip-annos>cname))
  

  classconstructor: superc -> JBCMethod(
	      [PUBLIC()]
	    , Init()
	    , JBCMethodDesc([], Void())
	    , [ ALOAD_0()
	      , INVOKESPECIAL(
	          JBCMethodRef(CRef(superc), MRef(Init()), JBCMethodDesc([], Void()))
	        )
	      , RETURN()
	      ]
	    )
	
  getstatic: (cref, fref, ftype) ->
  		GETSTATIC(
  			JBCFieldRef(
  				CRef(cref),
  				FRef(fref),
  				JBCFieldDesc(Reference(CRef(ftype)))
  			)
  		)

  invokevirtual: (cref, mref, lparams, ret) ->
  		INVOKEVIRTUAL(
          JBCMethodRef(
            CRef(cref)
          , MRef(mref)
          , JBCMethodDesc(lparams, ret)
          )
        )
  