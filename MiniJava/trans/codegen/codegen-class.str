module codegen/codegen-class

imports
  
  milestone1/MiniJava
  milestone2/MiniJava.core
  lib/jasmin/JasminXT
  trans/codegen/-
  libstratego-lib
  libstratego-aterm

rules
	
	// MainClass   : ID * ID * Statement -> MainClass
  class-to-jbc: MainClass(cname, pname, stmt) -> JBCFile(
  	CLASS-HEADER(<strip-annos> cname, superc)
	// Fields
	, []
  	// Methods
  	, [ DEFAULT-CONSTRUCTOR(superc)
  	  , JBCMethod(
	      [PUBLIC(), STATIC()]
	    , "main"
	    , JBCMethodDesc([Array(Reference(CRef("java/lang/String")))], Void())
	    , outputstmtjbc*
	    )
  	  ]
  	)
  	where
  		stmtjbc* := <stmt-to-jbc> stmt
  	;	stacklimit := <stack-limit;int-to-string> stmt
  	//;	stacklimit	:= 100
  	;	superc	 := <parentname> None()
  	;	outputstmtjbc* := [JBCLimitStack(stacklimit),stmtjbc*,RETURN()]
  	
  
  // Class       : ID * ParentDecl * List(FieldDecl) * List(MethodDecl) -> ClassDecl
  class-to-jbc:	Class(cname, parent, field*, method*) ->
  	JBCFile(
  		CLASS-HEADER(<strip-annos> cname,<parentname>parent)
		// Fields
		, <map(field-to-jbc)> field*
  		// Methods
  		,[DEFAULT-CONSTRUCTOR(<parentname> parent) | <map(method-to-jbc)> method*]
  	)
  	
  /** Field generation **/
  field-to-jbc: Field(fieldt, fieldn) -> JBCField([PUBLIC()], <strip-annos>fieldn, JBCFieldDesc(<type-to-jbc>fieldt), None(), None())
  	
overlays
	
	 	DEFAULT-CONSTRUCTOR(superc) =
	    JBCMethod(
	      [PUBLIC()]
	    , Init()
	    , JBCMethodDesc([], Void())
	    , [ JBCLimitStack("1"), JBCLimitLocals("1")
	      , ALOAD_0()
	      , INVOKE-CONSTRUCTOR(superc)
	      , RETURN()
	      ]
	    )
	
	INVOKE-CONSTRUCTOR(superc) =
		INVOKESPECIAL(
	    	JBCMethodRef(CRef(superc), MRef(Init()), JBCMethodDesc([], Void()))
	    )
	    
	CLASS-HEADER(cname, superc) =
		JBCHeader(
			    None()
			  , None()
			  , JBCClass([PUBLIC()], cname)
			  , JBCSuper(superc)
			  , []
			  , None()
			  , None()
			  , None()
		  )